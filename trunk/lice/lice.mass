#
#   IRC Script Program. For use with ircii-EPIC5 clients.
#   Copyright (C) 2000 SrfRoG (cag@codehack.com)
#
# ---------------------------------------------------------------------------
# All code by SrfRoG, unless specified. Visit http://lice.muppetz.com
# ---------------------------------------------------------------------------
# Updated for EPIC5 by tjh
# Support for halfop added by tjh 7/5/2012

IF (word(2 $loadinfo()) != [pf]) {
  LOAD -pf $word(1 $loadinfo());
  RETURN;
};

PACKAGE LiCe;

# Mass commands
ALIAS _proc.mass_match {
  get_user_match $C $1 temp_list;
  @ temp_list = remw($N $temp_list);
  FE ($temp_list) mm.1 {
    SWITCH ($0) {
      (+k) { PUSH $2 $mm.1 };
      (+o) { IF (ischanop($mm.1 $C)) { PUSH $2 $mm.1 } };
      (-h) { IF (ishalfop($mm.1 $C)) { PUSH $2 $mm.1 } };
      (+h) { UNLESS (ishalfop($mm.1 $C)) { PUSH $2 $mm.1 } };
      (-k) (-o) { UNLESS (ischanop($mm.1 $C)) { PUSH $2 $mm.1 } };
      (+v) { IF (ischanvoice($mm.1 $C) == 1) { PUSH $2 $mm.1 } };
      (-v) { IF (ischanvoice($mm.1 $C) == 0) { PUSH $2 $mm.1 } };
    };
  };
  ^ASSIGN -temp_list;
};

ALIAS mass {
  IF (@) {
    @ :mass.p = strlen($1) ? uhc($1) : [*!*];
    SWITCH ($0) {
      (-d) {
        _proc.mass_match +o $mass.p mass_list;
        _proc.loop_mode $winserv() - o $C mass_list;
      };
      (-n) {
        _proc.mass_match -h $mass.p mass_list;
        _proc.loop_mode $winserv() - h $C mass_list;
      };
      (-k) {
        @ mass_msg = strlen($2) ? [$2-] : randread($main.reason_kick);
        _proc.mass_match +k $mass.p mass_list;
        _proc.loop_kick $winserv() $C mass_list mass_msg;
      };
      (-l) {
        @ mass_msg = strlen($2) ? [$2-] : randread($main.reason_kick);
        _proc.mass_match -k $mass.p mass_list;
        _proc.loop_kick $winserv() $C mass_list mass_msg;
      };
      (-o) {
        _proc.mass_match -o $mass.p mass_list;
        _proc.loop_mode $winserv() + o $C mass_list;
      };
      (-h) {
        _proc.mass_match +h $mass.p mass_list;
        _proc.loop_mode $winserv() + h $C mass_list;
      };
      (-q) {
        _proc.mass_match +v $mass.p mass_list;
        _proc.loop_mode $winserv() - v $C mass_list;
      };
      (-v) {
        _proc.mass_match -v $mass.p mass_list;
        _proc.loop_mode $winserv() + v $C mass_list;
      };
    };
  }{
    uecho Usage: /MASS <-d|-o|-n|-h|-q|-v> [pattern];
    uecho Usage: /MASS <-k|-l> [pattern [reason]];
  };
};
#tjh/12
